#21.2e3.maple
#
# Frank Sottile
# 20 January 2001
# Amherst, MASS
#
# Here, we draw the maximally inflected quintics with one claw (singularity 
# of type 21) and three cusps.   There are two, and they are identical.
#
# 21 * 2 * 2 * 2
# oo   0   1  -1
#
interface(quiet=true):
with(plots):
plotsetup(x11,plotoptions=`width=3in,height=2.5in`):
read(`equations.maple`):
read(`EasySolve.maple`):

M:=matrix([
[1,s,s^2,  s^3,s^4  ,   s^5],
[0,1,2*s,3*s^2,4*s^3, 5*s^4],
[0,0, 1 ,3*s  ,6*s^2,10*s^3],
[0,0, 0 , 1   ,4*s  ,10*s^2],
[1,a, 0 , d   ,  0  ,  f   ],
[0,0, 1 , b   ,  0  ,  e   ],
[0,0, 0 , 0   ,  1  ,  c   ]]):

Coords:=matrix([
[1,a, 0 , d   ,  0  ,  f   ],
[0,0, 1 , b   ,  0  ,  e   ],
[0,0, 0 , 0   ,  1  ,  c   ]]):

Ker:={[-a, 1, 0, 0, 0, 0], [-f, 0, -e, 0, -c, 1], [-d, 0, -b, 1, 0, 0]}:
################################################################
#
#   Claw, cusp, cusp, cusp,...
#
#   This is for problem 21 * 2 * 2 * 2
#                       oo   0  1   -1 
#
S:=1:
equations:=[TwoEq(M,0)[],TwoEq(M,S)[],TwoEq(M,-1)[]]:
Eqs:=Groebner[gbasis](equations,plex(a,b,c,d,e,f)):
Polys:=MakePolys(Ker,Eqs):
z:=Polys[1][1]:
y:=Polys[1][2]:
x:=Polys[1][3]:

#plot({[s,z,s=-4..4],[s,y,s=-4..4],[s,x,s=-4..4]},-4..4,-10..10,color=[red,blue,green]);
#plot({[s,x-z,s=-3..3]},-3..3,-10..10,color=[magenta,black,blue]);

z:=x-z:
zero:=fsolve(z=0,s):
y:=(y-subs(s=zero-.0001,y/x)*x):
teps:=.1: seps:=.1:
Xs:=x/z:
Ys:=y/z:
Xt:=simplify(subs(s=1/t,Xs)):
Yt:=simplify(subs(s=1/t,Ys)):

plotsetup(gif,plotoutput=`21.2e3.gif`,plotoptions=`height=180,width=180`):
Q:=plot({[Xs,Ys,s=zero+seps..1],[Xt,Yt,t=1/zero+teps..1]},
          numpoints=200,color=blue,axes=none):
  display(Q);
###########################################################
