#
#  The Schubert Calculus tells us that given 3 general 3-planes A,B,C and 
#  3 general 2-planes X,Y,Z in 5-dimensional space, there are 4 partial flags 
#  H_2 < H_3 such that H_2 meets A,B, and X, and H_3 intersects C in a
#  2-dimensional subspace and also meets Y and X.
#
#  We compute the discriminant of the 4-parameter system obtained by fixing
#  X and C to osculate the rational normal curve at 0 and at infinity.
#  The discriminant factors, with one factor:
#  ( 2*E2 + 2*F2 - E1_F1), where E1, E2, (resp. F1, F2) are the elementary
#   symmetric polynomials in the parameters of the 2-planes Y,Z,
#  (resp. of the 3-planes A,B).
#
#  This shows that there are distinct values of the parameters where the
#  Schubert varieties do not meet transversally.
#
with(linalg):
with(Groebner):

Mat := s -> matrix([
	  [1 , s ,s^2, s^3 , s^4 ],    # The flag osculating the rational
 	  [0 , 1 ,2*s,3*s^2,4*s^3],    # normal curve at the point s.
	  [0 , 0 , 1 , 3*s ,6*s^2],    #
#   
	  [0 , 1 , b ,  0  ,  0  ],    #  Local coordinates for the partial
	  [1 , a , c ,  0  ,  0  ],    #  flag H_2 < H_3  
	  [0 , 0 , 0 ,  1  ,  d  ]]):  #  (Last 2 rows are H_2)

# A,B,Y,Z are the equations imposed by A,B,Y,Z on the flag H_2 < H_3.

A:=simplify(det(submatrix(Mat(s),[1,2,3,5,6],[1,2,3,4,5]))/s);
B:=simplify(det(submatrix(Mat(t),[1,2,3,5,6],[1,2,3,4,5]))/t);

Y:=simplify(det(submatrix(Mat(S),[1,2,4,5,6],[1,2,3,4,5]))/S/S);
Z:=simplify(det(submatrix(Mat(T),[1,2,4,5,6],[1,2,3,4,5]))/T/T);

G:=gbasis([A,B],tdeg(a,b,c,d));
Asol:=solve(G[1]=0,a):

YY:=simplify(8*s*t*subs(a=Asol,Y)):
ZZ:=simplify(8*s*t*subs(a=Asol,Z)):

H:=gbasis([YY,ZZ],tdeg(b,c,d)):
Csol:=solve(H[3]=0,c):
CC:=simplify(8*s*subs(a=Asol,B)/3):
CC:=simplify((8*t*s-3*b*d)*subs(c=Csol,CC)/2/t/s);

DD:=H[2];
#Q:=simplify((-3*b*d+8*t*s)*subs(c=Csol,H[4])/s/t):
#P:=simplify((-3*b*d+8*t*s)*subs(c=Csol,H[1])/s/t):
#H:=gbasis([P,Q,R],tdeg(b,d));
elim:=simplify(CC*coeff(DD,b) - DD*coeff(CC,b));
indets(elim);
Disc:=primpart(discrim(elim,d)):
fdisc:=factor(Disc):
nops(op(1,fdisc));
op(2,fdisc);