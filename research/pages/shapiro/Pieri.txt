
	Special Schubert Conditions and Shapiros' Conjecture:
		         A Challenge

		         Frank Sottile
		         16 April, 1997

This is a companion to "Hypersurface Schubert...".  In that, I gave some
deficient square systems of polynomials (rather MAPLE routines to
generate the polynomials) some of which had only real roots, and others
conjecturally have only real roots.  Here, I give MAPLE routines which
generate overdetermined systems of polynomials with the same properties.
This is both a challenge to you to replicate what I know, as well as to
apply your methods to what I do not know.

Rather than send the equations, I will send you pieces of MAPLE code.
These can be modified to give other sets of equations to test.

All of these systems are essentially alike, for instance, the first
system is equivalent to one of the form

                      [ s^6  6*s^5 15*s^4 20*s^3  1  0 ]
                      [ s^5  5*s^4 10*s^3 10*s^2  a  0 ]
                      [ s^4  4*s^3  6*s^2  4*s    b  0 ]
  (*)  Maximal Minors [ s^3  3*s^2  3*s     1     c  1 ] = 0,
                      [ s^2  2*s     1      0     0  d ]
                      [  s     1     0      0     0  e ]
                      [  1     0     0      0     0  f ]


for three real, non-zero values of s0, s2, s3.  For each s, the six
maximal minors describe a codimension 2 locus, thus we need three sets
of maximal minors, as there are six variables.

This is just the following problem in enumerative geometry, in local
coordinates: 

Find the 2-planes in C^7 which meet each of the 4-planes L(0), L(s1),
L(s2), L(s3), and L(\infty), where L(s) is the 4-plane in C^7 which
osculates the rational normal curve (first column).

The first four columns of the matrix give the 4-planes L(s) in R^7 which
osculate the rational normal curve.  The last two columns are local
coordinates in the Grassmannian of 2-planes in C^7 for the set of
2-planes which meet L(0) and L(\infty).  This choice of local
coordinates cuts down the number of variables from 10 to 6, perhaps
making this problem more tractable.

Since there is a projective transformation taking any two (in fact, any
three) points on the rational normal curve to any other two (three),
there is no loss of generality in suppose two points are 0 and infinity.

This can be formulated more generally:

Which p-planes in C^{m+p} meet general linear subspaces L_1,...,L_s,
where dim L_i = m+1-k_i and k_1+...+k_s = mp?

There are algorithms for determining the number of such p-planes.

These polynomial systems are devised to investigate Shapiros' conjecture
in this context:

If the linear subspaces L_i are chosen to osculate the rational normal
curve at distinct real points, then all of p-planes (a priori only
complex) meeting L_1, ..., L_s are real.


The MAPLE routines written here set up polynomial systems to investigate
Shapiros' conjecture in a number of instances.  Besides having chosen
coordinates to reduce the number of variables, I have also chosen a more
economical set of equations.   If K(s) is the matrix of linear forms
which define L(s), then it suffices to consider the equations

    E_j(s) :=   \Sum_\alpha \alpha(X) \alpha(K_j(s)),

where X is the matrix of local coordinates, j indexes sets of p columns
of K(s), \alpha runs over all sets of p rows, and \alpha(X),
respectively, \alpha(K_j(s)) is the maximal minor of X selected by
\alpha.


I am think it would be interesting to see how/if other methods work
on testing for real solutions.   Hence this note.


These systems are also interesting in that they give families of
overdetermined systems, all of which have the same number of solutions.

-Frank Sottile
 MSRI, Berkeley

###############################
#
#	Begin MAPLE 
#
##############################


##########################################################################
#
#	This MAPLE code is designed to set up the equations for
#       instances of Shapiros' conjecture concerning real enumerative
#       geometry. 
#
#	It treats the enumerative problem of 2-planes meeting 5
#	4-planes in C^7.
#
#        m:=2; p:=5;    k:=[2,2,2,2,2];          # no. sols = 6
#
#############################
interface(quiet=true):

with(linalg):

K := matrix([
[   1  ,   0  ,   0  ],
[-4*s  ,   1  ,   0  ],
[ 6*s^2,-4*s  ,   1  ],
[-4*s^3, 6*s^2,-4*s  ],
[   s^4,-4*s^3, 6*s^2],
[   0  ,   s^4,-4*s^3],
[   0  ,   0  ,   s^4]]):

#	L(\infty) = [0 0 0 * * * * *]
#	L(0)      = [* * * * * 0 0 0]
#
#	The 2-planes meeting L(0) and L(\infty) are parameterized by

X := matrix([
[1 , 0 ],
[a , 0 ],
[b , 0 ],
[c , 1 ],
[0 , d ],
[0 , e ],
[0 , f ]]):

BINOM:=[[1,2],[1,3],[1,4],[1,5],[1,6],[1,7],
[2,3],[2,4],[2,5],[2,6],[2,7],[3,4],[3,5],[3,6],[3,7],
[4,5],[4,6],[4,7],[5,6],[5,7],[6,7]]:
S3:=[1,2]:
S2:=[1,3]:
S1:=[2,3]:
  e3:=0:
  e2:=0:
  e1:=0:

for i from 1 to 3 do
  for S in BINOM do
   e.i:= e.i+det(submatrix(X,S,S3))* det(submatrix(K,S,S.i)):
  od: od:

Index:={1,2,3}:     #       Your values here!

equations:={}:
for jj in Index  do
for i from 1 to 3 do
equations:= equations union {subs(s=jj,e.i)}:
  od: od:

for ee in equations do
lprint(ee,`,`);od;
lprint(`0;`);

quit

##########################################################################
#
#	This MAPLE code sets the equations for instances of Shapiros' 
#	conjecture.
#
#	It treats the enumerative of 2-planes meeting 6 5-planes in C^8.
#	
#        m:=2; p:=6;    k:=[2,2,2,2,2,2];          # no. sols = 15
#
##############################
interface(quiet=true):

with(linalg):

K :=matrix([
[    1  ,    0  ,    0  ],
[ -5*s  ,    1  ,    0  ],
[ 10*s^2, -5*s  ,    1  ],
[-10*s^3, 10*s^2, -5*s  ],
[ 5*s^4 ,-10*s^3, 10*s^2],
[  -s^5 ,  5*s^4,-10*s^3],
[    0  ,   -s^5,  5*s^4],
[    0  ,    0  ,   -s^5]]):


#	L0  = [0 0 0 * * * * *]
#	L'0 = [* * * * * 0 0 0]
#
#	Then the 2-planes meeting L0 and L'0 are parameterized by

X := matrix([
[ 1 , 0],
[ a , 0],
[ b , 0],
[ c , 1],
[ d , e],
[ 0 , f],
[ 0 , g],
[ 0 , h]]):

BINOM:=[[1,2],[1,3],[1,4],[1,5],[1,6],[1,7],[1,8],
[2,3],[2,4],[2,5],[2,6],[2,7],[2,8],
[3,4],[3,5],[3,6],[3,7],[3,8],
[4,5],[4,6],[4,7],[4,8],[5,6],[5,7],[5,8],
[6,7],[6,8],[7,8]]:
S3:=[1,2]:
S2:=[1,3]:
S1:=[2,3]:
  e3:=0:
  e2:=0:
  e1:=0:

for i from 1 to 3 do
  for S in BINOM do
   e.i:= e.i+det(submatrix(X,S,S3))* det(submatrix(K,S,S.i)):
  od: od:

Index:={1,2,3,4}:     #       Your values here!

equations:={}:
for jj in Index  do
for i from 1 to 3 do
equations:= equations union {subs(s=jj,e.i)}:
  od: od:

for ee in equations do
lprint(ee,`,`);od;
lprint(`0;`);


quit


##########################################################################
#
#	This MAPLE code sets the equations for instances of Shapiros' 
#	conjecture.
#
#	It treats the enumerative of 2-planes meeting 7 6-planes in C^9.
#	
#        m:=2; p:=7;  k:=[2,2,2,2,2,2,2];      # no. sols = 36
#
##############################

interface(quiet=true):

with(linalg):

K:=  matrix([
[   0   ,   0   ,   1   ],
[   0   ,   1   , -6*s  ],
[   1   , -6*s  , 15*s^2],
[ -6*s  , 15*s^2,-20*s^3],
[ 15*s^2,-20*s^3, 15*s^4],
[-20*s^3, 15*s^4, -6*s^5],
[ 15*s^4, -6*s^5,    s^6],
[ -6*s^5,    s^6,    0  ],
[    s^6,    0  ,    0  ]]):


#	L0  = [0 0 0 * * * * *]
#	L'0 = [* * * * * 0 0 0]
#
#	Then the 2-planes meeting L0 and L'0 are parameterized by

X := matrix([
[ 1 ,  0],
[x1 ,  0],
[x2 ,  0],
[x3 ,  1],
[x4 , x6],
[x5 , x7],
[0  , x8],
[0  , x9],
[0  , x0]]):

BINOM:=[[1,2],[1,3],[1,4],[1,5],[1,6],[1,7],[1,8],[1,9],
[2,3],[2,4],[2,5],[2,6],[2,7],[2,8],[2,9],
[3,4],[3,5],[3,6],[3,7],[3,8],[3,9],
[4,5],[4,6],[4,7],[4,8],[4,9],[5,6],[5,7],[5,8],[5,9],
[6,7],[6,8],[6,9],[7,8],[7,9],[8,9]]:
S3:=[1,2]:
S2:=[1,3]:
S1:=[2,3]:
  e3:=0:
  e2:=0:
  e1:=0:

for i from 1 to 3 do
  for S in BINOM do
   e.i:= e.i+det(submatrix(X,S,S3))* det(submatrix(K,S,S.i)):
  od: od:

Index:={1,2,3,4,5}:     #       Your values here!

equations:={}:
for jj in Index  do
for i from 1 to 3 do
equations:= equations union {subs(s=jj,e.i)}:
  od: od:

for ee in equations do
lprint(ee,`,`);od;
lprint(`0;`);


quit


##########################################################################
#
#	This MAPLE code sets the equations for instances of Shapiros' 
#	conjecture.
#
#	It treats the enumerative of 3-planes meeting 6 3-planes in C^7.
#	
#        m:=4; p:=3;  k:=[2,2,2,2,2,2];      # no. sols = 16
#
##############################

interface(quiet=true):

with(linalg):


K:= transpose(matrix([
[1,-3*s, 3*s^2,  -s^3,  0   ,  0  , 0  ],
[0,  1 ,-3*s  , 3*s^2,  -s^3,  0  , 0  ],
[0,  0 ,  1   ,-3*s  , 3*s^2, -s^3, 0  ],
[0,  0 ,  0   ,  1   ,-3*s  ,3*s^2,-s^3]])):
 
 
#	L0  = [0 0 0 0 * * *]
#	L'0 = [* * * 0 0 0 0]
#
#	Then the 2-planes meeting L0 and L'0 are parameterized by

X := matrix([
[ 1 , 0  , 0 ],
[x1 , 1  , 0 ],
[x2 , x3 , 0 ],
[0  , x4 , 1 ],
[0  , x5 , 1 ],
[0  , x6 , x7],
[0  , 0  , x8]]):



BINOM:=[[1,2,3],[1,2,4],[1,2,5],[1,2,6],[1,2,7],
[1,3,4],[1,3,5],[1,3,6],[1,3,7],[1,4,5],[1,4,6],[1,4,7],
[1,5,6],[1,5,7],[1,6,7],
[2,3,4],[2,3,5],[2,3,6],[2,3,7],[2,4,5],[2,4,6],[2,4,7],
[2,5,6],[2,5,7],[2,6,7],
[3,4,5],[3,4,6],[3,4,7],
[3,5,6],[3,5,7],[3,6,7],
[4,5,6],[4,5,7],[4,6,7],[5,6,7]]:

S4:=[1,2,3]:
S3:=[1,2,4]:
S2:=[1,3,4]:
S1:=[2,3,4]:
  e4:=0:
  e3:=0:
  e2:=0:
  e1:=0:


for i from 1 to 4 do
  for S in BINOM do
   e.i:= e.i+det(submatrix(X,S,S4))* det(submatrix(K,S,S.i)):
  od: od:


Index:={1,2,3,4}:     #       Your values here!

equations:={}:
for jj in Index  do
for i from 1 to 4 do
equations:= equations union {subs(s=jj,e.i)}:
  od: od:

for ee in equations do
lprint(ee,`,`);od;
lprint(`0;`);


quit
