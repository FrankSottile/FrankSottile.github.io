
# Frank Sottile
# 30 Juin 1999
# Geneve
#
# Here, we compute maximally inflected real rational quartic curves,
# which gives a complete topological classification.
#
#   Here  i  is an ordinary inflection point and  k  is a cusp
#
#     values      points of inflection       Number with n nodes
#      i  k          i           k              0       1
#     ---------------------------------------------------------------
# 2)   4  1      -3 -1 1 3     infty            1       2
#
interface(quiet=true):
with(plots):
with(linalg):
with(Groebner):

RatNorm:=vector([t^4,t^3*s,t^2*s^2,t*s^3,s^4  ]):
#RatNorm:=vector([1,s,s^2,s^3,s^4  ]):


############################################################################
# 2) Cusp at infty


S:=1/2*(17+3*33^(1/2))^(1/2);

M2:=det(matrix([
[1,s,s^2,  s^3,s^4  ],
[0,1,2*s,3*s^2,4*s^3],
[0,0, 1 ,3*s  ,6*s^2],
[1,a, b , 0   ,  c  ],
[0,0, 0 , 1   ,  d  ]])):


GP2:=gbasis([M2,subs(s=t,M2),subs(s=u,M2),subs(s=v,M2)],plex(a,b,c,d)):
G2:=[]:
for i from 1 to nops(GP2) do
 G2:=[G2[],simplify(subs(s=-S,t=-1,u=1,v=S,GP2[i]))]:
od:

G2:=gbasis(G2,plex(a,b,c,d)):
D2:=[solve(G2[1]=0,d)]:
C2:=[solve(G2[2]=0,c),solve(G2[2]=0,c),solve(G2[2]=0,c)]:
B2:=[solve(subs(d=D2[1],G2[3])=0,b),solve(subs(d=D2[2],G2[3])=0,b),solve(subs(d=D2[3],G2[3])=0,b)]:
A2:=[solve(subs(d=D2[1],G2[4])=0,a),solve(subs(d=D2[2],G2[4])=0,a),solve(subs(d=D2[3],G2[4])=0,a)]:

for i from 1 to 3 do
 x2[i]:=multiply(vector([-C2[i],0,0,-D2[i],1]),RatNorm):
 y2[i]:=multiply(vector([-B2[i],0,1,0,0]),RatNorm):
 z2[i]:=multiply(vector([-A2[i],1,0,0,0]),RatNorm):
od:

X2[1]:=5*y2[1]/x2[1]:    Y2[1]:=5/2*z2[1]/x2[1]:
X2[2]:=x2[2]/(x2[2]+80*y2[2]-140*z2[2]):
Y2[2]:=140*z2[2]/(x2[2]+80*y2[2]-140*z2[2]):
X2[3]:=x2[3]/(x2[3]+80*y2[3]+140*z2[3]):
Y2[3]:=140*z2[3]/(x2[3]+80*y2[3]+140*z2[3]):

#X1[1]:=105/8*t^4+15/8*t^4*33^(1/2)+5*t^2*s^2 :
#Y1[1]:=5/2*t^3*s  :
#Z1[1]:=(51/4+9/4*33^(1/2))*t^4+s^4 :
#Eq1[1]:=4*x^4*33^(1/2)+20*x^4-288*x^2*y^2*33^(1/2)-1632*x^2*y^2
#+5487*y^4*33^(1/2)+31521*y^4-40*x^3+860*x*y^2+100*x*y^2*33^(1/2)+800*y^2:

#X1[2]:=(51/4+9/4*33^(1/2))*t^4+(3+33^(1/2))*t*s^3+s^4 :
#Y1[2]:=-105/2*t^4-35/2*t^4*33^(1/2)+140*t^3*s :
#Z1[2]:=261/4*t^4+79/4*t^4*33^(1/2)+3*t*s^3+t*s^3*33^(1/2)
#        +s^4+80*t^2*s^2-140*t^3*s:

Vals:=[[-S,1],[-1,1],[1,1],[S,1]]:
for i from 1 to 3 do
 Points[i]:=[]:
 for j from 1 to 4 do
  Points[i]:=[Points[i][],[subs(s=Vals[j][1],t=Vals[j][2],X2[i]),
                           subs(s=Vals[j][1],t=Vals[j][2],Y2[i])]]:
 od:
 P2[i]:=pointplot({Points[i][]},symbol=circle,color=red):
 Q2[i]:=plot({[subs(t=1,X2[i]),subs(t=1,Y2[i]),s=-1..1],
         [subs(s=1,X2[i]),subs(s=1,Y2[i]),t=-1..1]},axes=none,color=blue):
od:

plotsetup(x11,plotoptions=`width=3in,height=2.5in`):
#plotsetup(ps,plotoutput=`1_Cusp.0.eps`,
#         plotoptions=`color,portrait,width=2.4in,height=2.4in,noborder`):

plotsetup(gif,plotoutput=`4-infl.0.gif`,plotoptions=`height=200,width=200`);
display(Q2[1],P2[1]);
