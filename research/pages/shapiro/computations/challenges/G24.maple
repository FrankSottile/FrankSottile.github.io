#G24.maple
#
#   Here, we compute the universal eliminant for Shapiro's conjecture when
# m=4 and p=2,  AND the flags osculate the rational normal
# curve at points  s,t,u,-s,-t,-u.
#
interface(quiet=true):

with(linalg):
with(Groebner):

Eq := s -> simplify(det(matrix([
              [1, s ,s^2, s^3 , s^4 ,   s^5],
              [0, 1 ,2*s,3*s^2,4*s^3, 5*s^4],
              [0, 0 , 1 , 3*s ,6*s^2,10*s^3],
              [0, 0 , 0 ,  1  ,4*s  ,10*s^2],
              [1,x12,x13, x14 ,  0  ,  0   ],
              [0, 0,  1 , x24 , x25 , x26  ]]))/s):

Vals:= [s,-s,t,-t,u,-u]:
eqs:=[]:
for i from 1 to 6 do eqs:=[eqs[],Eq(Vals[i])]: od:

G:=gbasis(eqs,wdeg([10,20,30,40,50,60],[x12,x26,x13,x24,x25,x14])): 

# After computing a grobner basis, we to eliminante the variables by hand

H:=[]:                    
for i from 1 to nops(G) do  
  if (indets(G[i])minus {s,t,u,x12,x13,x24,x26})={} then H:=[H[],G[i]]: fi: 
od:

H:=gbasis(H,wdeg([10,20,30,50],[x12,x26,x13,x24])):

GG:=[]:     
for i from 1 to nops(H) do  
  if (indets(H[i]) minus {s,t,u,x12,x13,x26})={} then GG:=[GG[],H[i]]: fi:  
od:

GG:=gbasis(GG,wdeg([10,20,70],[x12,x26,x13])): #print(time(),nops(GG));

HH:=[]:
for i from 1 to nops(GG) do 
 if (indets(GG[i]) minus {s,t,u,x12,x26})={} then HH:=[HH[],GG[i]]: fi:  
od:

HH:=gbasis(HH,wdeg([5,60],[x12,x26])): #print(time(),nops(HH));
HH:=gbasis(HH,wdeg([5,66],[x12,x26])): #print(time(),nops(HH));

elim:=HH[2]:

#   Taking advantage of the fact the eliminant is a polynomial in x12^2
#elim:=subs(x12=x12^(1/2),elim):

Disc:=factor(discrim(elim,x12)):
nops(Disc);
lprint(op(6,Disc));
lprint(`##########################`);
lprint(op(5,Disc));
lprint(`##########################`);
lprint(op(4,Disc));
lprint(`##########################`);
lprint(op(3,Disc));
lprint(`##########################`);
lprint(op(2,Disc));
lprint(`##########################`);
lprint(op(1,Disc));
time();
quit;





