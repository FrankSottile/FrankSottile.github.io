//Resolutions.sing
//
//  This file shows some of Singular's commands for computing free resolutions
//
////////////////////////////////////////////////////////////////////////////////////////////////////
//    redSB computes a reduced Groebner basis
option(redSB);
//   sets random number generator for system
system("random",210);
//
//
ring S = 0, (x,y,z), dp;
//
//   First, a principal ideal
//
print("Compute the resolution of the principal ideal generated by x^3+y^3+z^3");
ideal I =  x^3+y^3+z^3;
resolution RI = mres( I, 0 );
RI;
print( betti(RI), "betti" );
//
//  Now add an indeterminate
//
print("Add the indeterminate x to the ideal and compute the resolution");
I = I + ideal(x);
RI = mres(I,0);
RI;
print( betti(RI), "betti" );
////////////////////////////////////////////////////////////////////////////////////////////////////
ring R = 0, (x,y,z,w), dp;
//
//  The rational normal curve
//
print("Let us look at the rational normal curve");
ideal J =  std( ideal( -x*z+y^2, -x*w+y*z, -y*w+z^2 ) );
dim(J), mult(J);
resolution SJ = sres( J, 0 );
SJ;
print( betti(SJ), "betti" );
//
//  The ideal of quadratic monomials
print("Now consider the ideal of all quadatic monomials");
J = maxideal(2);
SJ = mres( J, 0 );
SJ;
print( betti(SJ), "betti" );
//
//  A complete Intersection
print("Try a complete intersection of type (2,3,4)");
J =  x^2 + 2*x*y - 3*z*w + 5*z^2 + x*w,
     y^3 + 7*x^2*z - 11*x*z^2 + y^2*w, 
     z^4 - x^2*y^2 + 13*x*y*w^2 + 17*y*z^2*w + z*w^3 ;
SJ = lres( J, 0 );
SJ;
print( betti(SJ), "betti" );

