#construction.maple
#
# Frank Sottile
# 27 November 2004
# Mexico City
#
#################################################################
#
#    This is Frank's latest and hopefully successful attempt
# to compute the construction of four triangles with 88 common
# tangents, and maybe to improve the number to 90 or even mnore.
# 
#    Not only will this accomplish the construction, but it will 
# eventually generate pictures for a webpage detailing the construction
# and also for the paper that we are writing.
#
#######################################################################
interface(quiet=true):
read("procedures"):
################################################################################
Digits :=45:
#####################################################################
#
#   We begin with four lines l1, l2, l3, l4 having two common transversals, 
# x and y, which meet the line li at points xi and yi.  For each i=1..4, 
# we choose a plane Pi through li whose intersection with the hyperboloid 
# Hi through the other three lines is `convex'.  The we let m1 be the line
# in P1 tangent to H1 at the point x1, m2 the line in P2 tangent to H2 at the
# point y2, and m3 the line in P3 tangent to H3 at the point x3.
#
#  These lines {l1,m1}, {l2, m2}, {l3, m3} define 8 hyperboloids, and we look 
# at their intersections with the plane P4, as well as the line m4 in P4 tangent 
# to H4 at the point y4 (or x4, there is a choice here that we may exploit).
#
#   Our construction of 88 lines tangent to four triangles involves perturbing
# this configuration, doubling each line li to get (rather thin) triangles.
#
############################################################################
#
#    Lines are represented by two points on them, and hyperboloids by a
# regulus (family of lines) on them.
#
############################################################################
#
#   Step 0: The hyperbolic paraboloid, H1
#
############################################################################
H := (t, a) ->  [t, a, t*a]:
l1:=[H(-1/2,1),  H(1/2,1)]:
l2:=[H(-1/2,-1), H(1/2,-1)]:
l3:=[[1/2,1/2,1/4], [-1/2,-1/2,1/4]]:
l4:=[H(-1/2,0),  H(1/2,0)]:
#
# (Remove this comment later)
#     I reordered the lines to try and take advantage of convexity in 
#  my construction.  the old numbers were 2,4,1,3
#
lx := subs(t=0, MakeRegulus(l1, l4, l2, t)):
ly := subs(t=1, MakeRegulus(l1, l4, l2, t)):
##############################################################################
#read("plotH1.maple");
##############################################################################
#
#  The planes through the lines l1, l2, l3, l4 are parametrized by the points of
# intersection with either of two lines, which we call lv and lh.
#
lh:=t -> ParameterizeLine([ParameterizeLine(lx,4),ParameterizeLine(ly,4)],t):
lv:=t -> ParameterizeLine([ParameterizeLine(lx,-2),ParameterizeLine(ly,4)],t):
###############################################################################
#
# Pi is the plane containing li and the point pi
#
# These are the values of T for the four planes
#
p1:=lh(1/2):     #  1/2 gives a horizontal plane for P2  x-z=0
p2:=lh(1/2):     # This gives a horizontal plane for P4  x+z=0
p3:=lv(1/2):     # This gives a vertical plane for P1  x-y=0
p4:=lv(1/2):     # This gives a vertical plane for P3  y=0
#
###############################################################################
#
#  Equations for the planes
#
Eqs:=[PlaneEquation(l1,p1),PlaneEquation(l2,p2),
      PlaneEquation(l3,p3),PlaneEquation(l4,p4)]:

###############################################################################
#
#   First step of the construction.  Perturb l1 and the tangent
#  
c1:=traceHyperboloid(l4,l3,l2, Eqs[1]):
t1:=TangentLine(c1,0):
k1:=PerturbLine(l1,p1,100,5/100000000000):
m1:=perturbTan(t1,l1[2],1/10000000000000000000000,14/1000000000000):

###############################################################################
#
#   Planei.maple plots the picture of the construction in the ith plane
#
#read("Plane1.maple");
###############################################################################
#
#  Second step of the Construction
#
cl2:=traceHyperboloid(l3,l1,l4, Eqs[2]):
ck2:=traceHyperboloid(l3,k1,l4, Eqs[2]):
d2:=traceHyperboloid(l3,m1,l4, Eqs[2]):
t2:=TangentLine(cl2,0):

k2:=PerturbLine(l2,p2,-50,-3/40000):
m2:=perturbTan(t2,l2[1],7/100000000000,3/100000):

#read("Plane2.maple");
###############################################################################
#
#  Third step of the Construction
#
cll3:=traceHyperboloid(l1,l4,l2, Eqs[3]):
ckl3:=traceHyperboloid(k1,l4,l2, Eqs[3]):
clk3:=traceHyperboloid(l1,l4,k2, Eqs[3]):
ckk3:=traceHyperboloid(k1,l4,k2, Eqs[3]):
dml3:=simplify(subs(t=t/(4*t-1),traceHyperboloid(m1,l4,l2, Eqs[3]))):
dmk3:=simplify(subs(t=t/(4*t-1),traceHyperboloid(m1,l4,k2, Eqs[3]))):
dlm3:=traceHyperboloid(l1,m2,l4, Eqs[3]):
dkm3:=traceHyperboloid(k1,m2,l4, Eqs[3]):
t3:=TangentLine(cll3,1):

k3:=PerturbLine(l3,p3,50,-5/1000):
m3:=perturbTan(t3,l3[2],8/1000000,9/1000):

#read("Plane3.maple");

###############################################################################
#
#  Fourth step of the Construction
#
clll4:=traceHyperboloid(l1,l2,l3, Eqs[4]):
#cllk4:=traceHyperboloid(l1,l2,k3, Eqs[4]):
#clkl4:=traceHyperboloid(l1,k2,l3, Eqs[4]):
#clkk4:=traceHyperboloid(l1,k2,k3, Eqs[4]):
#ckll4:=traceHyperboloid(k1,l2,l3, Eqs[4]):
#cklk4:=traceHyperboloid(k1,l2,k3, Eqs[4]):
#ckkl4:=traceHyperboloid(k1,k2,l3, Eqs[4]):
ckkk4:=traceHyperboloid(k1,k2,k3, Eqs[4]):

dmll4:=simplify(subs(t=30*t/(100*t-37),traceHyperboloid(m1,l2,l3, Eqs[4]))):
#fsolve(denom(dmll4[1])=0,t); quit;
# 0.225, 0.37500
# 0.41,   0.54
#dmkl4:=simplify(subs(t=30*t/(100*t-37),traceHyperboloid(m1,k2,l3, Eqs[4]))):
#dmlk4:=simplify(subs(t=30*t/(100*t-37),traceHyperboloid(m1,l2,k3, Eqs[4]))):
#dmkk4:=simplify(subs(t=30*t/(100*t-37),traceHyperboloid(m1,k2,k3, Eqs[4]))):

dlml4:=traceHyperboloid(l1,m2,l3, Eqs[4]):
#dlmk4:=traceHyperboloid(l1,m2,k3, Eqs[4]):
#dkml4:=traceHyperboloid(k1,m2,l3, Eqs[4]):
#dkmk4:=traceHyperboloid(k1,m2,k3, Eqs[4]):

dllm4:=traceHyperboloid(m3,l2,l1, Eqs[4]):
#dlkm4:=traceHyperboloid(m3,l2,k1, Eqs[4]):
#dklm4:=traceHyperboloid(m3,k2,l1, Eqs[4]):
#dkkm4:=traceHyperboloid(m3,k2,k1, Eqs[4]):

t4:=TangentLine(clll4,0):

k4:=PerturbLine(l4,p4,10,32/100):
m4:=perturbTan(t4,l4[2],1/100,-3/40):

read("Plane4.maple");

T:=[Triangle(l1,k1,m1), Triangle(l2,k2,m2), Triangle(l3,k3,m3), Triangle(l4,k4,m4)]:

for i from 1 to 4 do 
 l[i]:=[T[i][1],T[i][2]]:
 k[i]:=[T[i][1],T[i][3]]:
 m[i]:=[T[i][2],T[i][3]]:
end do:

numberOfTangents(T);

