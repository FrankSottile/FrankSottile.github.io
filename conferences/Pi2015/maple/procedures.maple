#procedures.maple
#
#  procedures for bar graphs for Pi day
#
############################################################
makeBarGraph := proc(Prs, piFun, myColor)
#
#  Prs := pairs of data
#  piFun := function that computes pi from the data
#  myColor := color
#
 global Hi, Wi, nBins:
 local S, i, m, M, avg, len, Bin, s, Mbin, Step, Polg, ii, Vert, AVG, Avg, PiVal, PI, Top, L:
 #
 #  Compute the pi approximates
 #
 S:= [seq(evalf(piFun(Prs[i][])),i=1..nops(Prs))];
 #
 #   Find min and max
 #
 M:=max(S):
 m:=min(S):
 avg:=sum(S[i],i=1..nops(S))/nops(S):
 #
 #   initialize the frequency table
 #
 len:=(M-m)/nBins:
 Bin:=[seq(0,i=1..nBins)]:
 for s in S do
  for i from 1 to nBins do
    if (s >= (i-1)*len+m and s < m+i*len) then Bin[i]:=Bin[i]+1: end if:
  end do:
 end do:
 #
 # The logic above skipped the biggest
 #
 Bin[nBins]:=Bin[nBins]+1:
 Mbin:=max(Bin):
 #
 #  Starts to draw 
 #
 Step:=Wi/nBins:
 #
 #   Make the polygons
 #
 Polg:=[]:
 for ii from 1 to nBins do 
  Vert[ii]:=[[Step*(ii-1),20],[Step*ii,20],[Step*ii,20+(Hi-40)*Bin[ii]/Mbin],
             [Step*(ii-1),20+(Hi-40)*Bin[ii]/Mbin]]:
  Polg:=[Polg[],polygonplot(Vert[ii],color=myColor)];
 end do:
 #
 AVG:=(avg-m)/(M-m)*Wi:
 PiVal:=(Pi-m)/(M-m)*Wi:
 Avg:=line([AVG,17],[AVG,Hi-20],color=blue,thickness=3):
 PI:=line([PiVal,20],[PiVal,Hi-17],color=brown,thickness=3):
 Top:=line([0,Hi-20],[Wi,Hi-20],color=magenta,thickness=1, linestyle=dash):
 L:=textplot({[0,0,sprintf("%4.2f",m)],[Wi,0,sprintf("%4.2f",M)],
             [AVG,0,sprintf("%5.3f",avg),'color'="Blue"],[PiVal,Hi-5,sprintf("pi",M),'color'="Brown"],
	     [-15,Hi-20,Mbin],[-20,2*Hi/3,sprintf("Tot=%d",nops(S))]}):
 return([Polg[],Avg,PI,L,Top])
end proc: 
###############################################################

