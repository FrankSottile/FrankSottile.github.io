#  The Schubert Calculus tells us that given 3 general 3-planes A,B,C and 
#  3 general 2-planes a,b,c in 5-dimensional space, there are 4 partial
#  flags  X < Y such that X meets a, B, and C and Y intersects A in
#  a 2-dimensional subspace and also meets b and c.
#
# We show that if A,B,C,a,b,c are chosen to osculate the standard rational 
# normal curve at the (respective) points 41, 58, 29, -97, 82, 21, then not 
# all the solutions are real.   This disproves the conjecture of Shapiro
# and Shapiro, in the original form they stated.
#
with(linalg):
with(Groebner):
readlib(realroot):
Digits:= 5:
Mat := s -> matrix([
	   [1 , s ,s^2, s^3 , s^4 ],    # The flag osculating the rational
 	   [0 , 1 ,2*s,3*s^2,4*s^3],    # normal curve at the point s.
	   [0 , 0 , 1 , 3*s ,6*s^2],    #
#   
	   [0 , 0 , 1 , x14 , x15 ],    #  Local coordinates for the partial
	   [1 , 0 ,x23, x24 , x25 ],    #  flag X < Y  
	   [0 , 1 ,x33, x34 , x35 ]]):  #  (Last 2 rows are X)

# W.i are the equations imposed by W on the flag X < Y.
# For A.i and a.i, the other 2 maximal minors are redundant.

B:=det(submatrix(Mat(-1),[1,2,3,5,6],[1,2,3,4,5])):
C:=det(submatrix(Mat(3),[1,2,3,5,6],[1,2,3,4,5])):

A1:=det(submatrix(Mat(0),[1,2,3,4,5],[1,2,3,4,5])):
A2:=det(submatrix(Mat(0),[1,2,3,4,6],[1,2,3,4,5])):
A3:=det(submatrix(Mat(0),[1,2,3,5,6],[1,2,3,4,5])):

a1:=det(submatrix(Mat(4),[1,2,5,6],[1,2,3,4])):
a2:=det(submatrix(Mat(4),[1,2,5,6],[1,2,3,5])):
a3:=det(submatrix(Mat(4),[1,2,5,6],[1,2,4,5])):

b:=det(submatrix(Mat(1),[1,2,4,5,6],[1,2,3,4,5])):
c:=det(submatrix(Mat(-5),[1,2,4,5,6],[1,2,3,4,5])):

G:=gbasis([A1,A2,A3,B,C,a1,a2,a3,b,c],
wdeg([1,5,5,5,5,5,5,5],[x14,x15,x23,x24,x25,x33,x34,x35])):

lprint(G[1]);
fsolve(G[1],x14,complex);
quit;
