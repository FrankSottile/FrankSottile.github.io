#33.instances
#  
# This maple file is set up to run instances of 
# Shapiro's conjecture for Grass(3,3).
#  
# We work in local coordinates for the set of 3-planes which meet
# the 3-planes K_3(0) and K_3(\infty).
#
# We create a Singular input file with the resulting equations, then run it
# to create a MAPLE file to test for real roots.
#
# We first tested the first 20 of the 36 7-tuples in [-3,-2,-1,1,2,3,4,5,6]
#   CAROL took 59807 sec. to compute elimninants using FGLM.
#   Schur ran the last 36.
#  It too 5753 sec. to compute eliminants, 
#     and 250 sec. to verify that all had 42 real roots.
#
# We next tested the first 64 of the 120 7-tuples in 
#		                    [-6,-5,-4,-3,1,2,3,5,7,11]
#
#                             time     size 
# Computing eliminants      34608     136732     
# Checking all roots real    1747     251793
#
#  Next, we did the 120 7-tuples in [-4,-3,-2,-1,1,2,3,7,8,9]
#
#                            time      size 
# Computing eliminants     59139      247832
# Checking all roots real   2959      427791
# 
#  Next, we did the 330 7-tuples in [1,2,3,4,5,6,7,8,9,10,11]
#
#                            time      size 
# Computing eliminants     339848     721271
# Checking all roots real   30557    1656479
# 
#
#25.550
#
interface(quiet=true):

ST_Nreal:="lprint(`realroots = `,NREAL,`Vals = `,":
ST_end:=");":

with(combinat):
#with(Ore_algebra):
with(linalg):
with(Groebner):

#trials:=choose([-4,-3,-2,-1,1,2,3,7,8,9],7):
#trials:=choose([-3,-2,-1,1,2,3,4,5,6],7):
trials:=choose([1,2,3,4,5,6,7,8,9,10,11],7):
#nops(trials);   quit;

Eq := s -> simplify(det(matrix([
              [1,s,s^2, s^3 , s^4 ,   s^5],
              [0,1,2*s,3*s^2,4*s^3, 5*s^4],
              [0,0, 1 , 3*s ,6*s^2,10*s^3],
              [1,a, b ,  0  ,  0  ,  0   ],
              [0,1, c ,  d  ,  e  ,  0   ],
              [0,0, 0 ,  1  ,  f  ,  g   ]]))/s):


#############################################################################
#
#

#lprint(`timer = 1;`);
lprint(`option(redSB);`);
lprint(`int t=timer;`);
lprint(`ring R= 0, (a,b,c,d,e,f,g), dp;`);
lprint(`ideal I;`);
lprint(`ideal H;`);
lprint(`ring S= 0, (g,f,e,d,c,b,a), (dp(6),dp(1));`);
lprint(`ideal G;`);
lprint(`print("interface(quiet=true):");`);
lprint(`print("readlib(realroot):");`);

for ntimes from 1 to nops(trials) do

 Vals:=trials[ntimes]:
 lprint(`setring R;`);
 lprint(`I = `);

 for i from 1 to 7 do  
 lprint(Eq(Vals[i]),`,`);  od:
 lprint(`0;`);
 lprint(`H = std(I);`);
 lprint(`setring S;`);
 lprint(`G  = fglm(R,H);`);
 lprint(`print("POLY:=");`);
 lprint(`short=0;`);
 lprint(`G[1];`);
 lprint(`print(":");`);
 lprint(`print("NREAL:= nops(realroot(POLY, 1/100)): ");`);
 lprint(`print(" if NREAL<42 then");`);
 lprint(`print("`,convert(ST_Nreal,symbol));
 lprint(convert(Vals,symbol));
 lprint(convert(ST_end,symbol),` ");`);
 lprint(`print("fi: ");`);
 lprint(`print(" ");`);
 lprint(`print(" ");`);
 lprint(`print("############################################################");`);
 lprint(`print("#                                                          #");`);
 lprint(`print("#          Next One                                        #");`);
 lprint(`print("#                                                          #");`);
 lprint(`print("############################################################");`);
 lprint(`print(" ");`);
od:

lprint(`print("time(); ");`);
lprint(`print("quit; ");`);
lprint(`timer-t;`);
lprint(`quit;`);

time();

quit;

