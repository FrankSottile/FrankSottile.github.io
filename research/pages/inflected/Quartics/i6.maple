#i6.maple
#
# Frank Sottile
# 4 October 2000
# Amherst
#
# We compute the 5 maximally inflected real rational quartic curves
# with 6 points of inflection, but consider what happens as two
# inflection points collide.   I can only parameterize this reasonably
# for s in the interval [2.5,...,1).
#
interface(quiet=true):
with(plots):
with(stats):
Digits:=30:

RatNorm:=linalg[vector]([t^4,t^3*s,t^2*s^2,t*s^3,s^4]):
Ifl:=[-3,-1,1,S]:
plotsetup(x11,plotoptions=`width=3in,height=2.5in`):

############################################################################
#  Inflection points at 0 and infinity

#
A := s ->  simplify(linalg[det](linalg[matrix]([
[1, s ,s^2,  s^3,  s^4],
[0, 1 ,2*s,3*s^2,4*s^3],
[0, 0 , 1 ,3*s  ,6*s^2],
[1, a , b ,  0  ,  0  ],
[0, 0 , 1 ,  c  ,  d  ]]))/s):
#
#  We used Singular to compute the ideal of solutions to the polynomial 
# system   A(-3)=A(-1)=A(1)=A(s)=0,  which gives the curves with 
# inflection points at 0, infinity, -3, -1, 1, and s.
#
G[1]:=d^5+(9*s+3)*d^4+(-8*s^2+24*s-72)*d^3+(-48*s^3+56*s^2-408*s-144)*d^2+(-96*s^3+720*s^2-864*s)*d+(1296*s^3+432*s^2):
G[2]:=(-288*s^3+960*s^2-288*s)*c-d^4+(-3*s-3)*d^3+(62*s^2-18*s+72)*d^2+(52*s^2-60*s+144)*d+(-648*s^3-216*s^2):
G[3]:=(-432*s^2-144*s)*b-d^4+(-9*s-3)*d^3+(8*s^2-24*s+72)*d^2+(48*s^3-56*s^2+408*s+144)*d+(96*s^3-720*s^2+864*s):
G[4]:=(576*s^3-1920*s^2+576*s)*a-d^4+(-3*s-3)*d^3+(62*s^2-18*s+72)*d^2+(52*s^2-60*s+144)*d+(-288*s^4+1176*s^3-3384*s^2+864*s):
#
# We set up about 80 points between 1 and 3 at which we evaluate these 
# solutions and then call least squares to find degree 12 polynomials
# that fit the data in this range.
#
Sol:=[[],[],[],[],[]]: points:=[[],[],[],[],[]]:
Var:=[]:
for j from 39 to 119  do
 S:=j/40:
 DD:= [fsolve(subs(s=S,G[1])=0,d)]:
 Var:=[Var[],S]:
 for i from 1 to 5 do
#  Sol[i]:=[Sol[i][],DD[i]]:
#  Sol[i]:=[Sol[i][],fsolve(subs(s=S,d=DD[i],G[2])=0,c)]:
#  Sol[i]:=[Sol[i][],fsolve(subs(s=S,d=DD[i],G[3])=0,b)]:
  Sol[i]:=[Sol[i][],fsolve(subs(s=S,d=DD[i],G[4])=0,a)]:
  points[i]:=[points[i][],[S,DD[i]]]:
 od:
od:
#
# Rather than do the least squares, we have stored the data in a file
#
#Eq12[i]:=op(2,fit[leastsquare[[s,y],y=a+b*s+c*s^2+d*s^3+e*s^4+f*s^5+g*s^6+h*s^7+k*s^8+l*s^9+m*s^10+A*s^11+B*s^12]]([Var,Sol[i]])):
#
read(`i6.eqs.maple`):
#
#
for i from 1 to 5 do
 X[i]:=linalg[multiply](linalg[vector]([B[i]*DD[i],0,-DD[i],0,1]),RatNorm):
 Y[i]:=linalg[multiply](linalg[vector]([B[i]*C[i],0,-C[i],1,0]),RatNorm):
 Z[i]:=linalg[multiply](linalg[vector]([-A[i]     ,1,0,0,0]),RatNorm):
od:

#  This will have no zeroes in the interval in question
X[5]:=X[5]+3/2*Y[5]-2*Z[5]:

for i from 1 to 5 do 
 x[i]:=Z[i]/X[i]:
 y[i]:=Y[i]/X[i]:
od:
i:=5:
animate( {[T^(3/2)*subs(t=1,x[i]),T^(3/2)*subs(t=1,y[i]), s=-1..1],
           [T^(3/2)*subs(s=1,x[i]),T^(3/2)*subs(s=1,y[i]), t=-1..1]}, T=0.9..3.1 );

