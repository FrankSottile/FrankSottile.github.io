#f1k1i2.maple
#
# Frank Sottile
# 29 September 1999
# Amherst
#
# Here, we draw the two maximally inflected real rational quartic curves
# with one flat point, one cusp, and 2 inflection points.
#
#
interface(quiet=true):
with(plots):
with(linalg):
with(Groebner):

RatNorm:=vector([t^4,t^3*s,t^2*s^2,t*s^3,s^4  ]):


M1:=det(matrix([
[1,s,s^2,  s^3,s^4  ],
[0,1,2*s,3*s^2,4*s^3],
[0,0, 1 ,3*s  ,6*s^2],
[1,a, b , 0   ,  0  ],
[0,0, 0 , 1   ,  0  ]])):

#######################################################################
# 1)  Flat point at 0, cusp at infinity, and inflections at -1, 1     #
#######################################################################
G:=gbasis([subs(s=-1,M1),subs(s=1,M1)],plex(a,b)):
#   G := [2 b + 1, a]
Z[1]:=multiply(vector([1/2, 0, 1, 0, 0]),RatNorm):
Y[1]:=multiply(vector([ 0 , 1, 0, 0, 0]),RatNorm):
X[1]:=multiply(vector([ 0 , 0, 0, 0, 1]),RatNorm):

######################################################################
# 2) Flat point at 0, cusp at infty, and inflections at 1, 3   #
#################################################################
G:=gbasis([subs(s=3,M1),subs(s=1,M1)],plex(a,b)):
#kernel(matrix([
#[1,a, b , 0   ,  0  ],
#[0,0, 0 , 1   ,  0  ]]));
Z[2]:=multiply(vector([-3/2, 0, 1, 0, 0]),RatNorm):
Y[2]:=multiply(vector([-3/2 , 1, 0, 0, 0]),RatNorm):
X[2]:=multiply(vector([ 0 , 0, 0, 0, 1]),RatNorm):

Asy:=[-1, 1]:
Yc:=[0, -3]:

for i from 1 to 2 do
 x[i]:=Z[i]/(X[i]+Yc[i]*Y[i]-Asy[i]*Z[i]):
 y[i]:=Y[i]/(X[i]+Yc[i]*Y[i]-Asy[i]*Z[i]):
od:

x[2]:=x[2]/(1-5*y[2]):
y[2]:=y[2]/(1-5*y[2]):
x[2]:=x[2]-y[2]:

for i from 1 to 2 do
 FP[i]:=pointplot({[subs(t=1,s=0,x[i]),subs(t=1,s=0,y[i])]},symbol=circle,color=green):
 Q[i]:=plot({[subs(t=1,x[i]),subs(t=1,y[i]),s=-1..1],
             [subs(s=1,x[i]),subs(s=1,y[i]),t=-1..1]},axes=none,color=blue):
od:

P[1]:=pointplot({[subs(t=1,s=1,x[1]),subs(t=1,s=1,y[1])],
                 [subs(t=1,s=-1,x[1]),subs(t=1,s=-1,y[1])]},symbol=circle,color=red
#           ,title=`Flat point at 0, cusp at infinity, and inflections at 1, -1`
):
P[2]:=pointplot({[subs(t=1,s=1,x[2]),subs(t=1,s=1,y[2])],
                 [subs(t=1,s=3,x[2]),subs(t=1,s=3,y[2])]},symbol=circle,color=red
#           ,title=`Flat point at 0, cusp at infinity, and inflections at 1, 3`
):

plotsetup(x11,plotoptions=`width=3in,height=2.5in`):

#plotsetup(gif,plotoutput=`f1k1i2.0.gif`,plotoptions=`height=180,width=180`):

plotsetup(ps,plotoutput=`f1k1i2.0.eps`,plotoptions=`color,portrait,width=3in,height=3in,noborder`);
display(Q[1],P[1],FP[1]);

#plotsetup(gif,plotoutput=`f1k1i2.1.gif`,plotoptions=`height=180,width=180`):

plotsetup(ps,plotoutput=`f1k1i2.1.eps`,plotoptions=`color,portrait,width=3in,height=3in,noborder`);
display(Q[2],P[2],FP[2]);

