//
//   Pluecker coordinates
//
option(redSB);
int k=2;  // This code assumes that k=2.
int n=9;  // This works with any n >= 4
int i,j;

///////////////////////////////////  Pluecker ring
string ringDec="ring R = 0, (";
for (i=1; i<n; i=i+1)
{
  for (j=i+1; j<=n; j=j+1)
  {
    if (i==n-1 and j==n)
    {
      ringDec = sprintf("%sp(%s,%s)), dp;",ringDec,i,j);
    }  else {
      ringDec = sprintf("%sp(%s,%s),",ringDec,i,j);
    }
  }
}
//////////////////////////////////////////////////////// Make the ring of Pluecker coordinates
execute(ringDec);
string bigRingDec = sprintf("ring S = 0, (x(1..2)(1..%s),%s), dp;",n,varstr(R));
execute(bigRingDec);
////////////////////////////////////////////////////////
///      Product of x-variables
poly Pxv=1;
for (i=1; i<=n; i=i+1)
 {
 Pxv = Pxv * x(1)(i)*x(2)(i);
 }
//////////////////////////////////////////////////////
/////////////////////////////////////////////////////
setring S;
S;
ideal I=0;
for (i=1; i<n; i=i+1)
 {
 for (j=i+1; j<=n; j=j+1)
 {
   I = I + ideal( p(i,j)- (x(1)(i)*x(2)(j)-x(2)(i)*x(1)(j)));
 }
}
I = std(I);
dim(I), mult(I);
///////////////////   Eliminate
ideal J = std(eliminate(I,Pxv));
dim(J), mult(J);
setring R;
ideal K = std(imap(S,J));
dim(K),mult(K);
//K;
quit;