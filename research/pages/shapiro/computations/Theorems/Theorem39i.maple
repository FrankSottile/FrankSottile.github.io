#Theorem39i.maple
#
#    Frank Sottile
#    August 3 1998
#    Berkeley, California
#
# This MAPLE script establishes Theorem 3.9.i of "Real Schubert Calculus:
# Polynomial systems and a conjecture of Shapiro and Shapiro".  This is the
# case of Shapiro's conjecture concerning the 3 2-planes in C^6 which meet 4
# 3-planes osculating the rational normal curve 
#
#interface(quiet=true):
with(Groebner):
with(linalg):
#
#   We choose coordinates for the set of 2-planes in C^6 which meet the
# 3-planes osculating the rational normal curve at 0 and at infinity.    
# This reduces the problem to one of six equations in 4 variables.   
# These equations are the independent maximal minors of the matrix:
#
SmMat := s -> matrix([
		[ 1 , s ,s^2, s^3 , s^4 ,  s^5 ],
		[ 0 , 1 ,2*s,3*s^2,4*s^3, 5*s^4],
		[ 0 , 0 , 1 , 3*s ,6*s^2,10*s^3],
		[ 1 ,x12,x13,  0  ,  0  ,   0  ],
		[ 0 , 0 , 0 ,  1  , x25 , x26  ]]):
#
#   We compute all maximal minors of this matrix.  The entries of 0 in
# positions x14 and x15 give these extra factors of s, which we remove.
#
AA:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[1,2,3,4,5]))/s):
BB:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[1,2,3,4,6]))/s):
CC:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[1,2,3,5,6]))/s/s):
DD:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[1,2,4,5,6]))/s/s/s/s):
EE:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[1,3,4,5,6]))/s/s/s/s):
FF:=simplify(det(submatrix(SmMat(s),[1,2,3,4,5],[2,3,4,5,6]))/s/s/s/s/s):
#
#  We generate the equations for the 2-plane to meet the 3-planes asculating
# at s and at t, and then compute the resulting Gr\"obner basis.
#
equations:=[]:
for P in [AA,BB,CC,DD,EE,FF] do
 equations:=[equations[],P,subs(s=t,P)]:
od:
#
G:=gbasis(equations,wdeg([1,4,4,4],[x12,x13,x25,x26])):
#
#   The computation proceeds over the function field R(s,t) and the 
# Gr\"obner basis consists of 4 polynomials, with the first the
# universal eliminant, a univariate polynomial in x25 of degree 3
#
UniversalEliminant:=G[1];
#
# We compute the Discriminant of the UniversalEliminant. 
#
Discriminant:=discrim(UniversalEliminant,x12);
#
# The discriminant is a sum of squares:
#
simplify(Discriminant - 
	( 450*(s-t)^6 + 450*s^6 + 450*t^6 + 1150*(s*t^2-t*s^2)^2 ) );
#
#   It follows that the number of real roots to the system is independent of
# the choice of real values for s and t.  Thus we need only show that there
# are 3 real roots for a single choice of the parameters.
#
solve(subs(s=1,t=2,UniversalEliminant)=0);
quit;
