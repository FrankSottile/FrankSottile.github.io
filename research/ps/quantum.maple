#
#   Frank Sottile
#   Madison, Wisconsin
#   23 April 1999
#
#    This Maple V.5 file computes the example reported in Section 4 of "Real
# rational curves in Grassmannians".   Here, we work with the quantum
# Grassmannian  of  rational curves of degree 1 in the Grassmannian of
# 2-planes in  4-space.  This is a complete intersection of 3 quadratic
# forms, and the  enumerative problem consists of 8 particular linear forms
# H(t) on this  space. 
#
#   We show that if our linear forms are H(i) for i=1,...,8, then there are
# no real solutions, but if the forms are H(i^6), then all 8 solutions are
# real.
#
#interface(quiet=true):
with(Groebner):
readlib(realroot):

# The coordinates for the Pl\"ucker space are
# z120, z130, z140, z230, z240, z340, z121, z131, z141, z231, z241, z341
# We work in the local coordinates given by z341 = 1.

# The coefficients of s^2, st, and t^2 of this polynomial are the equations
# for the quantum Grassmannian

Pl:=(z120+z121*t)*(z340+t)-(z130+z131*t)*(z240+z241*t)
                     +(z140+z141*t)*(z230+z231*t):

# The linear form H(t) is given below.

H  := t ->  t^4*(z120*t^4-2*z130*t^3+z140*t^2+3*z230*t^2-2*z240*t+z340)
           +(z121*t^4-2*z131*t^3+z141*t^2+3*z231*t^2-2*z241*t+1):

# We construct the ideals.  G1 <--> i, and G2 <--> i^6.

G1:=[subs(t=0,Pl),coeff(Pl,t),coeff(Pl,t^2)]:
G2:=[subs(t=0,Pl),coeff(Pl,t),coeff(Pl,t^2)]:

for i from 1 to 8 do
  G1:=[G1[],H(i)]:
  G2:=[G2[],H(i^6)]:
od:

# We compute an eliminatioin Gr\"obner basis for G1, and the number of real
#  roots of the, eliminant GG[1] is the number of real solutions. 

GG:=gbasis(G1,wdeg([1,9,9,9,9,9,9,9,9,9,9],
      [z230,z130,z120,z140,z240,z340,z121,z131,z141,z231,z241])):

lprint(`When t_i=i, there are `, nops(realroot(GG[1])),` real solutions`);

# We compute an eliminatioin Gr\"obner basis for G2

GG:=gbasis(G2,wdeg([1,9,9,9,9,9,9,9,9,9,9],
      [z230,z130,z120,z140,z240,z340,z121,z131,z141,z231,z241])):

lprint(`When t_i=i^6, there are `, nops(realroot(GG[1])),` real solutions`);
time();

quit;
#################################################
#################################

    |\^/|     Maple V Release 5 
._|\|   |/|_. Copyright (c) 1981-1997 by Waterloo Maple Inc. All rights
 \  MAPLE  /  reserved. Maple and Maple V are registered trademarks of
 <____ ____>  Waterloo Maple Inc.
      |       Type ? for help.
#
#   Frank Sottile
#   Madison, Wisconsin
#   23 April 1999
#
#    This Maple V.5 file computes the example reported in Section 4 of "Real
# rational curves in Grassmannians".   Here, we work with the quantum
# Grassmannian  of  rational curves of degree 1 in the Grassmannian of
# 2-planes in  4-space.  This is a complete intersection of 3 quadratic
# forms, and the  enumerative problem consists of 8 particular linear forms
# H(t) on this  space. 
#
#   We show that if our linear forms are H(i) for i=1,...,8, then there are
# no real solutions, but if the forms are H(i^6), then all 8 solutions are
# real.
#
> with(Groebner):
> readlib(realroot):
> 
# The coordinates for the Pl\"ucker space are
# z120, z130, z140, z230, z240, z340, z121, z131, z141, z231, z241, z341
# We work in the local coordinates given by z341 = 1.
> 
# The coefficients of s^2, st, and t^2 of this polynomial are the equations
# for the quantum Grassmannian
> 
> Pl:=(z120+z121*t)*(z340+t)-(z130+z131*t)*(z240+z241*t)
>                      +(z140+z141*t)*(z230+z231*t):
> 
# The linear form H(t) is given below.
> 
> H  := t ->  t^4*(z120*t^4-2*z130*t^3+z140*t^2+3*z230*t^2-2*z240*t+z340)
>            +(z121*t^4-2*z131*t^3+z141*t^2+3*z231*t^2-2*z241*t+1):
> 
# We construct the ideals.  G1 <--> i, and G2 <--> i^6.
> 
> G1:=[subs(t=0,Pl),coeff(Pl,t),coeff(Pl,t^2)]:
> G2:=[subs(t=0,Pl),coeff(Pl,t),coeff(Pl,t^2)]:
> 
> for i from 1 to 8 do
>   G1:=[G1[],H(i)]:
>   G2:=[G2[],H(i^6)]:
> od:
> 
# We compute an elimination Gr\"obner basis for G1, and the number of real
#  roots of the, eliminant GG[1] is the number of real solutions. 
> 
> GG:=gbasis(G1,wdeg([1,9,9,9,9,9,9,9,9,9,9],
>       [z230,z130,z120,z140,z240,z340,z121,z131,z141,z231,z241])):
> 
> lprint(`When t_i=i, there are `, nops(realroot(GG[1])),` real solutions`);
When t_i=i, there are    0    real solutions
> 
# We compute an elimination Gr\"obner basis for G2
> 
> GG:=gbasis(G2,wdeg([1,9,9,9,9,9,9,9,9,9,9],
>       [z230,z130,z120,z140,z240,z340,z121,z131,z141,z231,z241])):
> 
> lprint(`When t_i=i^6, there are `, nops(realroot(GG[1])),` real solutions`);
When t_i=i^6, there are    8    real solutions
> time();
                                     1.570
> 
> quit;
bytes used=12727784, alloc=10287268, time=1.57
