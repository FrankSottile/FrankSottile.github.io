#C0_spline.maple
#
#  This file makes the animation of the two C_0 splines
#
with(plots):
with(plottools):
plotsetup(x11):
#
#  Here are the polynomials defining the edges of the domain cells
#
edges:=[x, (x-1)^2+(y+1)^2-2, x^2+(y-1)^2-1 ]:
#
#  The basis of the module of C^0 splines
#
#| 1 x2y+y3+2x2-4y        x3+xy2-2xy     |
#| 1 x2y+y3-2x2-2xy+4x-4y x3+xy2-2x2+2xy |
#| 1 0                    0              |
#
#  There are two cubic C_0 splines, stored
#
F:=[x^2*y+y^3+2*x^2-4*y, x^2*y+y^3-2*x^2-2*x*y+4*x-4*y, 0]:
factor(F[1]-F[2]),factor(F[1]),factor(F[2]);
G:=[x^3+x*y^2-2*x*y, x^3+x*y^2-2*x^2 + 2*x*y, 0]:
factor(G[1]-G[2]),factor(G[1]),factor(G[2]);
#
#  These have vanishing third component, we add a constant spline to them and 
#    scale to give splines that look better
#quit;
#
SP:=[seq((F[i]-x^2*y-y^3+y-x)/3,i=1..3)]:  # C0_spline.I
#SP:=[seq((-F[i]+y^3/2-x/3)*0.8,i=1..3)]:  # C0_spline.II
###################################################################
SplI := proc(r,t,Sp)
local X,Y,Th:
 Th := t*(Pi-arcsin(r/2)) + (1-t)*(3*Pi/2):
 X:= r*cos(Th):
 Y:= r*sin(Th):
 return([X,Y,subs(x=X,y=Y, Sp[1])])
end proc:
###################################################################
SplII := proc(r,t,Sp)
local X,Y,Th:
 Th := t*(-Pi/2) + (1-t)*(arccos(r/2/sqrt(2))-Pi/4):
 X:= r*cos(Th):
 Y:= r*sin(Th):
 return([X,Y,subs(x=X,y=Y, Sp[2])])
end proc:
###################################################################
SplIII := proc(r,t,Sp)
local X,Y,Th:
 Th := t*(arccos(r/2/sqrt(2))-Pi/4) + (1-t)*(Pi-arcsin(r/2)):
 X:= r*cos(Th):
 Y:= r*sin(Th):
 return([X,Y,subs(x=X,y=Y, Sp[3])])
end proc:
###################################################################

Edges:=[
    spacecurve(SplI(1,t,SP) , t=0..1, thickness=2,color=red,numpoints=15),
    spacecurve(SplII(1,t,SP) , t=0..1, thickness=2,color=red,numpoints=15),
    spacecurve(SplIII(1,t,SP) , t=0..1, thickness=2,color=red,numpoints=15)]:
Rays:=[
    spacecurve(SplI(r,0,SP) , r=0..1, thickness=3,color=brown,numpoints=11),
    spacecurve(SplII(r,0,SP) , r=0..1, thickness=3,color=brown,numpoints=11),
    spacecurve(SplIII(r,0,SP) , r=0..1, thickness=3,color=brown,numpoints=11)]:
GI:= plot3d(SplI(r,t,SP) ,r=0..1, t=0..1,grid=[11,15],color=cyan):
GII:= plot3d(SplII(r,t,SP) ,r=0..1, t=0..1,grid=[11,15],color=yellow):
GIII:= plot3d(SplIII(r,t,SP) ,r=0..1, t=0..1,grid=[11,15],color=green):
a:=-0.53:
aa:=[a,a,a]:
BaseI:=plot3d(SplI(r,t,aa) ,r=0..1, t=0..1,grid=[11,15],color=cyan, style=patchnogrid):
BaseII:=plot3d(SplII(r,t,aa) ,r=0..1, t=0..1,grid=[11,15],color=yellow, style=patchnogrid):
BaseIII:=plot3d(SplIII(r,t,aa) ,r=0..1, t=0..1,grid=[11,15],color=green,
    style=patchnogrid):
BEdges:=[
    spacecurve(SplI(r,0,aa) , r=0..1, thickness=3,color=brown,numpoints=11),
    spacecurve(SplII(r,0,aa) , r=0..1, thickness=3,color=brown,numpoints=11),
    spacecurve(SplIII(r,0,aa) , r=0..1, thickness=3,color=brown,numpoints=11),
    spacecurve(SplI(1,t,aa) , t=0..1, thickness=2,color=red,numpoints=15),
    spacecurve(SplII(1,t,aa) , t=0..1, thickness=2,color=red,numpoints=15),
    spacecurve(SplIII(1,t,aa) , t=0..1, thickness=2,color=red,numpoints=15)]:
SPointIII:=evalf(SplII(1,0,SP)):
Lines:=[line([0,0,a],[0,0,0], color=black, numpoints=2),
        line(SplI(1,0,SP),[SplI(1,0,SP)[1..2][],a], color=black, numpoints=2),
        line(SPointIII,[SPointIII[1..2][],a], color=black, numpoints=2),
        line(SplIII(1,0,SP),[SplIII(1,0,SP)[1..2][],a], color=black, numpoints=2),
	NULL]:
SPL:=[GI,GII,GIII,Edges[],Rays[],BaseI,BaseII,BaseIII,BEdges[],Lines[]]:

Pic:= ang -> display(SPL,orientation=[ang,70]):#,style=patchnogrid):

#
#  These make the movies for the WWW page
#
NFr:=180:
#plotsetup(gif,plotoutput="movie.gif",plotoptions=`height=700,width=600`): 
plotsetup(gif,plotoutput="med.gif",plotoptions=`height=350,width=300`): 
#
#NFr:=120:
#plotsetup(gif,plotoutput="sm.gif",plotoptions=`height=250,width=200`): 
animate(Pic,[ang],ang=0..(1-1/NFr)*360,scaling=constrained,axes=none,frames=NFr,paraminfo=false);
 
 

