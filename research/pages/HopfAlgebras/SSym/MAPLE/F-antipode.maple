#F-antipode-html.maple
#
#  This computes the antipode in the F-basis, printing out a .html file
#
#  We use the formula
#      m ( s \otimes 1 )\delta F_w = 0
#  to compute the antipode recursively
#  S[w]:=antipode of F_w
interface(quiet=true):
read(`MalvenutoProcedures`):
read(`HtmlProcedures`):
###############################################################
#
#   The coproduct in the F-basis
#
FCoProd := proc(w)
local k, FCP:
 FCP:=[]:
 for k from 0 to nops(w) do
  FCP:=[FCP[],[1,Split(w,k)[]]]:
 od:
FCP
end:
#################################################################
#
# This takes a pair of permutations and returns the list representing the
#   product of the F-functions.
#
FProd := proc(u,v)
local perm, L:
 L:=[]:
 for perm in Shuffle(u,v) do
  L:=[L[],[1,perm]]:
 od:
 L
end:
########################################################

#file:=fopen(`../F-Antipode.html`,WRITE):
file:=fopen(`F-Antipode.html`,WRITE):

fprintf(file,`<HTML>\n`):
fprintf(file,`<HEAD>\n`):
fprintf(file,`   <TITLE>Antipode of the Fundamental Basis of SSym</TITLE>\n`):
fprintf(file,`</HEAD>\n`):
fprintf(file,`<BODY BGCOLOR="#FFFFFF">\n\n`):

fprintf(file,`<H1>Antipode of the Fundamental basis of <B>S</B><I>Sym</I></H1>\n\n`):
fprintf(file,`<HR COLOR="#0000FF" size=2 noshade>\n\n`):

fprintf(file,`<!------------------------------------------------------------->\n\n`):


S[[]] :=[[1,[]]]:

for n from 1 to 5 do 
 fprintf(file,cat(`<FONT SIZE=+1 COLOR="5c3317"><B>Antipodes of total degree `,n)):
 fprintf(file,`</B></FONT><BR>\n\n`):
 fprintf(file,`<TABLE>\n`):
 for w in combinat[permute](n) do
  CoProdW:=FCoProd(w):
  N:=nops(CoProdW):
  S[w]:=[]:
  ###########################################################
  #   CoProdW has the form [ [1, [], w], ..., [1, w, []] ]
  ###########################################################
  for j from 1 to N-1 do
   ##################################################################
   #  Loops over the non-final terms [ Coeff, x, v] of the coproduct
   ##################################################################
   Term:=CoProdW[j]:
   x := Term[2]:
   v := Term[3]:
   for L in S[x] do
   #######################################################################
   #  Loops over terms [ Coeff, u] in the antipode of the permutation x
   #######################################################################
    u:=L[2]: 
    for pair in FProd(u,v) do
     ##########################################################################
     #  Loops over the terms in the product F_u*F_v and makes the coefficients
     ##########################################################################
     pair[1]:=-Term[1]*L[1]*pair[1]:
     S[w]:=[S[w][],pair]:
    od:
   od:
  od:
  fprintf(file,cat(`   <TR VALIGN=TOP><TD>S(`,HtmlTerm([1,w],"<B>F</B>"))):
  fprintf(file,cat(`)&nbsp; &nbsp; =</TD>\n`)):
  fprintf(file,cat(`    <TD>&nbsp; &nbsp; `,Format(Combine(S[w]),"<B>F</B>"))):
  fprintf(file,cat(`</TD></TR>\n`)):
 od:
 fprintf(file,`</TABLE>\n\n`):
 fprintf(file,`<!----------------------------------------------------------->\n`):
 fprintf(file,cat(`<HR size=1 noshade width=800 align=left>\n`)):
 fprintf(file,`<!----------------------------------------------------------->\n\n`):
od:


fprintf(file,`<HR COLOR="#0000FF" size=2 noshade>\n\n`):
fprintf(file,`<I><FONT COLOR="#BB00FF">Created 5 August 2002</font></I>\n`):
fprintf(file,`</BODY>\n</HTML>\n`):

fclose(file):

time();
quit:
